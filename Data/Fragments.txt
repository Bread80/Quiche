;This file contains code fragments which are used by the compiler to
;generate primitives and intrinsics

;Data can be inserted into a fragment using substitiions.
;Substitutions are placed in angle brackets, e.g.
;inc <p1.r8>

;Substitutions are split into two parts: prefix and suffix.
;The prefix specifies which parameter the data is to be for.
;Valid values are:
;d.		Destination
;p1.	First parameter
;p2.	Second parameter
;vars.	Data about variables and parameters used when generating stack frames

;Suffix specifies the actual data required from the parameter
;Valid values are:
;immbyte	- immediate value as a byte
;immword	- immediate value as a word
;r8			- an 8 bit register
;r16		- a 16 bit register pair
;r16low		- the low register of a register pair
;r16high	- the high register of a register pair
;offset		- the offset to a stack variable
;offsetlow	- offset to the low byte of a stack variable
;offsethigh	- offset to the high byte of a stack variable
;varname	- Fully qualified name of the variable

;Variables and stackframe data
;paramsbytesize	- number of bytes allocated on the stack for parameters by the current function
;localsbytesize	- number of bytes allocated on the stack for local variables by the current function

;Invalid values will result in a compiler exception or assertion failure.

;In addition the $$ parameter can be used to susbitute meta data into the comments
;of the output code. The exact data to be substituted is dependant on the 
;substitutions above but is usually the variable name.

;Validation
;===============================================
;Validate the result of an operation, or that a type conversion
;will fit into the destination type.

;Raise overflow if carry flag set
=c_overflow
  jp c,raise_overflow

;Raise overflow if carry flag clear
=nc_overflow
  jp nc,raise_overflow
  
;Raise overflow if zero flag clear
=nz_overflow
  jp nz,raise_overflow
  
;Raise overflow if sign flag clear (result is positive, bit 7 clear)
;Useful when converting signed to unsigned and vice versa
=p_overflow
  jp p,raise_overflow
  
;Raise overflow if sign flag set (result is negative, bit 7 set)
=m_overflow
  jp m,raise_overflow
  
;Raise overflow if parity/overflow even (indicating signed overflow)
=pe_overflow
  jp pe,raise_overflow

=hreg_nz_overflow
  ld a,h
  and a
  jp nz,raise_overflow
  
;Loads  
;====================================================
;Load data from a constant or variable into a register

=load_r8_imm
  ld <p.r8>,<p.immbyte>

=load_r16_imm
  ld <p.r16>,<p.immword>
  
=load_r16high_zero
  ld <p.r16high>,$00
  
  
=load_r8_abs_a
  ld a,(<p.varname>)	;$$

=load_r8_abs_via_a
  ld a,(<p.varname>)	;$$
  ld <p.r8>,a

=load_r8_abslow_a
  ld a,(<p.varname>)	;$$
  
=load_r8_abslow_via_a
  ld a,(<p.varname>)	;$$
  ld <p.r8>,a

=load_r8_abshigh_a
  ld a,(<p.varname> + 1)	;$$
  
=load_r8_abshigh_via_a
  ld a,(<p.varname> + 1)	;$$
  ld <p.r8>,a
 
=load_r16_abs
  ld <p.r16>,(<p.varname>)	;$$
  
=load_r16low_abslow_a
  ld a,(<p.varname>)	;$$

=load_r16low_abslow_via_a
  ld a,(<p.varname>)	;$$
  ld <p.r16low>,a  


=load_r8_rel
  ld <p.r8>,(ix<p.offset>)	;$$

=load_r8_rellow
  ld <p.r8>,(ix<p.offsetlow>)	;$$
  
=load_r8_relhigh
  ld <p.r8>,(ix<p.offsethigh>)	;$$
    
=load_r16_rel
  ld <p.r16low>,(ix<p.offsetlow>)	;$$  
  ld <p.r16high>,(ix<p.offsethigh>)

=load_pr16low_rellow
  ld <p.r16low>,(ix<p.offsetlow>)	;$$



;Assigns
;===================================================
;Assigns are used to (usually) set varaibles from constant values.

;Where an assign can directly use an immediate value it will. If not
;the Primitive will specify a register or choice of registers to place
;value into and the Assign will store the value in that register. The
;end result here is that if the Assign is storing a value from a register
;it will use the /Param1/ register, whereas a store will use the Dest 
;register. The Primitive will use the code generator load-before-primitive
;functionality to put the value into that register.

;PS. Most of these are a hangover from the days when numeric constants where
;stored by the compiler in Z80 type sized variables. They're now in x86 sized
;variables, but may well be needed on a Z80 hosted compiler so worth keeping 
;around

=assign_abs8_a
  ld (<d.varname>),a		;$$

=assign_abs16_r16
  ld (<d.varname>),<p1.r16>	;$$  
  
=assign_abs16_imm8_neg
  ld a,<p1.immbyte>
  ld (<d.varname>),a		;$$
  ld a,$ff
  ld (<d.varname> + 1),a

=assign_abs16_imm8
  ld a,<p1.immbyte>
  ld (<d.varname>),a		;$$
  xor a
  ld (<d.varname> + 1),a

=assign_abs16_imm16
  ld a,<p1.immwordlow>
  ld (<d.varname>),a		;$$
  ld a,<p1.immwordhigh>
  ld (<d.varname> + 1),a
 

=assign_rel8_imm8
  ld (ix<d.offset>),<p1.immbyte>	;$$ 
  
=assign_rel16_imm8
  ld (ix<d.offsetlow>),<p1.immbyte>	;$$
  ld (ix<d.offsethigh>),$00

=assign_rel16_imm8_neg
  ld (ix<d.offsetlow>),<p1.immbyte>	;$$
  ld (ix<d.offsethigh>),$ff

=assign_rel16_imm16
  ld (ix<d.offsetlow>),<p1.immwordlow>	;$$
  ld (ix<d.offsethigh>),<p1.immwordhigh>



=assign_abs16_r8
;TODO: ONLY FOR A!
  ld (<d.varname>),<p1.r8>	;$$
  xor a
  ld (<d.varname> + 1),a

=assign_abs8_r8
;TODO: ONLY FOR A!
  ld (<d.varname>),<p1.r8> 	;$$ assign_abs8_r8

=assign_abs8_r16low
;TODO: ONLY FOR A!
  ld (<d.varname>),<p1.r16low>   ;$$
  

=assign_rel16_r8
  ld (ix<d.offsetlow>),<p1.r8>	;$$
  ld (ix<d.offsethigh>),$00

=assign_rel8_r8
  ld (ix<d.offset>),<p1.r8> 	;$$

=assign_rel8r_16low
  ld (ix<d.offset>),<p1.r16low>   ;$$


;Stores
;====================================================
;A store copies the result of a primitive into a variable

=store_abs8_a
  ld (<d.varname>),a		;$$
  
=store_abs8_r8_a
  ld (<d.varname>),a		;$$
  
=store_abs8_r8_via_a
  ld a,<d.r8>
  ld (<d.varname>),a	;$$


=store_abs16_r8_a
  ld (<d.varname>),a
  xor a
  ld (<d.varname> + 1),a	;$$
 
=store_abs16_r8_via_a
  xor a
  ld (<d.varname> + 1),a	;$$
  ld a,<d.r8>
  ld (<d.varname>),a
  
;Store to low byte of absolute variable
;For use before a sign extend
=store_abs16low_r8_a
  ld (<d.varname>),a	;$$

=store_abs16low_r8_via_a
  ld a,<d.r8>
  ld (<d.varname>),a	;$$
  
;Store to high byte of absolute variable
;For use after a sign extend
=store_abs16high_a
  ld (<d.varname> + 1),a		;$$

;For use after a sign extend
=store_abs16high_via_a
  ld a,<d.r8>
  ld (<d.varname> + 1),a	;$$

=store_abs16_r16
  ld (<d.varname>),<d.r16>	;$$

=store_abs8_r16low_via_a
  ld a,<d.r16low>
  ld (<d.varname>),a	;$$
  
  
=store_rel8_a
  ld (ix<d.offset>),a        ;$$  
  
=store_rel8_r8
  ld (ix<d.offset>),<d.r8>	;$$

=store_rel8_r16low
  ld (ix<d.offsetlow>),<d.r16low>	;$$
    
  
=store_rel16_r8
  ld (ix<d.offsethigh>),$00	;$$
  ld (ix<d.offsetlow>),<d.r8>

;For use before a sign extend
=store_rel16low_r8
  ld (ix<d.offsetlow>),<d.r8>	;$$

;For use after a sign extend
=store_rel16high_a
  ld (ix<d.offsethigh>),a		;$$
  
;For use after a sign extend
=store_rel16high_r8
  ld (ix<d.offsethigh>),<d.r8>	;$$

=store_rel16_r16
  ld (ix<d.offsetlow>),<d.r16low>	;$$
  ld (ix<d.offsethigh>),<d.r16high>

;Stack operations
;================

=stacklocal_enter
  ld hl,-<vars.localsbytesize>
  call stacklocal_enter
  
=stacklocal_exit
;ld de,return_value_if_any
  ld hl,<vars.paramsbytesize> + $0004	;+ Stack frame size (return addr, prev IX)
  jp stacklocal_exit

  
;Push byte to the stack
=push_byte_a
  push af
  inc sp
  
=push_word
  push <d.r16>
    
;ALU operations
;=========================================================

;----Addition

;8 bit add to immediate
=add8_reg_imm
  add a,<p2.immbyte>

;8 bit add to register
=add8_reg_reg
  add a,<p2.r8>

;16 bit add. Can only test for unsigned overflow due to ADD HL,ss opcode
=add16_nooverflow
  add hl,<p2.r16>	
  
;16 bit add. Affects all needed flags for validation and 16 bit conversion
=add16_overflow
  or a				;Clear carry
  adc hl,<p2.r16>

=inc8_reg
  inc <p1.r8>

=inc16_reg
  inc <p1.r16>

=dec8_reg
  dec <p1.r8>

=dec16_reg
  dec <p1.r16>
  
;----Subtraction

;8 bit immediate subtract
=sub8_reg_imm
  sub a,<p2.immbyte>

;8 bit register subtract
=sub8_reg_reg
  sub a,<p2.r8>

;16 bit subtract
=sub16
  and a
  sbc hl,<p2.r16>	
  
;----Negation

=negate_s8
  neg				;Parity flag set if A was $80

;=negate_u16
;  ld hl,$0000
;  and a
;  sbc hl,de

;----Comparisons
;Note: We need to set A for assignments, flags for branches

=compare8_reg_imm
  cp <p2.immbyte>

;Swapped parameter ordering
=compare8swap_reg_imm
  cp <p1.immbyte>

=compare8_reg_reg
  cp <p2.r8>
  
;Swapped parameter ordering
=compare8swap_reg_reg
  cp <p1.r8>  
  
;Compare and set A to zero if equal
=equalx16
  and a				;Clear carry
  sbc hl,<p2.r16>
  ld a,h			;Set to zero if result was equal
  or l

;p1 < p2 for boolean operands. Equivalent to !p1 AND p2
=bool_less8_reg_imm
  cpl
  and <p2.immbyte>
  
;p1 < p2 for boolean operands. Equivalent to !p1 AND p2
=bool_less8_reg_reg
  cpl
  and <p2.r8>

;p1 > p2 for boolean operands. Equivalent to p1 AND !p2
;NOTE: To generate this we need to reverse the order of the operands!
=bool_greater8_reg_imm
  cpl
  and <p1.immbyte>
  
;p1 > p2 for boolean operands. Equivalent to p1 AND !p2
;NOTE: To generate this we need to reverse the order of the operands!
=bool_greater8_reg_reg
  cpl
  and <p1.r8>

;p1 <= p2 for boolean operands. Equivalent to !p1 OR p2
=bool_lessequal8_reg_imm
  cpl
  or <p2.immbyte>
  
;p1 <= p2 for boolean operands. Equivalent to !p1 OR p2
=bool_lessequal8_reg_reg
  cpl
  or <p2.r8>
  
;p1 >= p2 for boolean operands. Equivalent to p1 OR !p2
;NOTE: To generate this we need to reverse the order of the operands!
=bool_greaterequal8_reg_imm
  cpl
  or <p1.immbyte>
  
;p1 >= p2 for boolean operands. Equivalent to p1 OR !p2
;NOTE: To generate this we need to reverse the order of the operands!
=bool_greaterequal8_reg_reg
  cpl
  or <p1.r8>
  
=odd8_reg
  rra
  sbc a,a
  
=odd8_reg_branch
  rra
  
;====Conversions to Boolean

;If A is non-zero outputs A=$ff, otherwise A=$00 (unchanged)
=atoboolean
  neg				;Set carry if A not equal 0
  sbc a,a			;Set A to -1 or 0
  
;If A is non-zero, outputs A=$00, otherwise A=$ff
=notatoboolean
  neg				;Set carry if A not equal 0
  sbc a,a			;Set A to -1 or 0
  cpl				;Invert bits of A
  
=atozf
  and a
  
;If Zero flag set, outputs A=$ff, otherwise A=$00
=zftoboolean
  ld a,0
  jr nz,$+3
  dec a

;If zero flag is clear, outputs A=$00, otherwise A=$ff
=nzftoboolean
  ld a,0
  jr z,$+3
  dec a
  
;If carry set, outputs A=$ff, otherwise A=$00
=cftoboolean
  sbc a,a
  
;If carry set, outputs A=$ff, otherwise A=$ff
=ncftoboolean
  sbc a,a
  cpl
  
;====Logic (and other boolean stuff)

;Complement the A register (i.e. bitwise invert)
=cpla
  cpl

=or8_reg_imm
  or <p2.immbyte>
  
=or8_reg_reg
  or <p2.r8>
  
=or16_reg_imm
  ld a,<p1.r16low>
  or <p2.immwordlow>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  or <p2.immwordhigh>
  ld <p1.r16high>,a
  
=or16_reg_reg
  ld a,<p1.r16low>
  or <p2.r16low>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  or <p2.r16high>
  ld <p1.r16high>,a

=and8_reg_imm
  and <p2.immbyte>
  
=and8_reg_reg
  and <p2.r8>
  
=and16_reg_imm
  ld a,<p1.r16low>
  and <p2.immwordlow>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  and <p2.immwordhigh>
  ld <p1.r16high>,a
  
=and16_reg_reg
  ld a,<p1.r16low>
  and <p2.r16low>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  and <p2.r16high>
  ld <p1.r16high>,a
  
=xor8_reg_imm
  xor <p2.immbyte>

=xor8_reg_reg
  xor <p2.r8>
  
=xor16_reg_imm
  ld a,<p1.r16low>
  xor <p2.immwordlow>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  xor <p2.immwordhigh>
  ld <p1.r16high>,a
  
=xor16_reg_reg
  ld a,<p1.r16low>
  xor <p2.r16low>
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  xor <p2.r16high>
  ld <p1.r16high>,a
  
;16 bit complement
=cpl16_reg
  ld a,<p1.r16low>
  cpl
  ld <p1.r16low>,a
  ld a,<p1.r16high>
  cpl
  ld <p1.r16high>,a  
  
=swap_reg_reg
  ld a,<p1.r16low>
  ld <p1.r16low>,<p1.r16high>
  ld <p1.r16high>,a
  
;=== Hardware

=in_reg_bc
  in <d.r8>,(c)
  
=in_a_n
  in a,(<p1.immbyte>)
  
=out_bc_reg
  out (c),<p2.r8>
  
=out_n_a
  out (<p1.immbyte>),a

;Loads and stores read or write to/from variables whereas peeks and pokes
;read or write unmanaged memory
=peek8_imm
  ld a,(<p1.immword>)
  
=peek8_reg16
  ld <d.r8>,(hl)
  
=peek16_imm
  ld hl,(<p1.immword>)
  
=peek16_reg16
  ld e,(hl)
  inc hl
  ld d,(hl)
  
=poke_imm_a
  ld (<p1.immword>),a
  
=poke_hl_imm8
  ld (hl),<p2.immbyte>
  
=poke_hl_reg8
  ld (hl),<p2.r8>

=poke_hl_reg16low
  ld (hl),<p2.r16low>
  
;!!at pokes, at the moment, are 8 bit
;=poke_imm_hl
;  ld (<p1.immword>),hl
  
;=poke_hl_imm16
;  ld (hl),<p2.immwordlow>
;  inc hl
;  ld (hl),<p2.immwordhigh>
  
;=poke_hl_reg16
;  ld (hl),<p2.r16low>
;  inc hl
;  ld (hl),<p2.r16high>